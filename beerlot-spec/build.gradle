plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.2'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'org.openapi.generator' version '6.0.1'
}

group = 'com.beerlot'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.24'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.7.2'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.7.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.13.3'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.3'
    implementation 'io.swagger.codegen.v3:swagger-codegen-maven-plugin:3.0.34'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.7.2'
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/beerlot-spec/spec/beerlot-api-spec.yaml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = 'com.beerlot.api.generated.api'
    modelPackage = 'com.beerlot.api.generated.model'
    templateDir = "$rootDir/beerlot-spec/templates".toString()
    configOptions = [
            dateLibrary: 'java11',
            useTags: 'true',
            interfaceOnly: 'true',
            hideGenerationTimestamp: 'true',
            useBeanValidation: 'false'
    ]
    importMappings = [
            'BeerPage': 'com.beerlot.spec.util.page.BeerPage'
    ]
}

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/src/main/java"
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate